FILESEXTRAPATHS_prepend := "${THISDIR}/files:"
FILESEXTRAPATHS_prepend := "${THISDIR}/files/mathworks:"

PLNX_DEPLOY_DIR ?= "${TOPDIR}/../images/linux"

# This file is included in the top level devicetree generated by Petalinux
SRC_URI += "file://system-user.dtsi"

# MathWorks devicetree sources
SRC_URI += "file://axilite.dts"
SRC_URI += "file://axistream.dts"
SRC_URI += "file://axistream_64.dts"

# MathWorks devicetree includes
SRC_URI += "file://base.dtsi"
SRC_URI += "file://zynqmp-mw-fpga-bridge-common.dtsi"
SRC_URI += "file://zynqmp-mw-common.dtsi"
SRC_URI += "file://mw-aximm-common.dtsi"
SRC_URI += "file://zynqmp-mw-axistream-iio-common.dtsi"
SRC_URI += "file://zynqmp-mw-axistream-iio-common-64.dtsi"
SRC_URI += "file://zynqmp-mw-cma.dtsi"
SRC_URI += "file://adi-mw-axistream-dma.h"
SRC_URI += "file://adi-mw-axistream-dma.dtsi"
SRC_URI += "file://adi-mw-axistream-iio-common.dtsi"
SRC_URI += "file://mw-axistream-iio-common.dtsi"
SRC_URI += "file://mw-axistream-iio-common.h"
SRC_URI += "file://mw-sharedmemory-iio.dtsi"

# Copy MW sources to devicetree workspace so they will be compiled
do_configure_append() {
	# This is run from the build directory so dts files will be one level up
	for DTS_FILE in `ls ../*.dts`; do
		cp ${WORKDIR}/${DTS_FILE#*/} ${DT_FILES_PATH}/devicetree_${DTS_FILE#*/}
	done
}

# Override deploy so that MW devicetrees are handled separately from system.dtb
do_deploy() {
	for DTB_FILE in `ls *.dtb *.dtbo`; do
		if [ "$DTB_FILE" = "system-top.dtb" ]; then
			install -Dm 0644 ${B}/${DTB_FILE} ${DEPLOYDIR}/${DTB_BASE_NAME}.${DTB_FILE#*.}
			ln -sf ${DTB_BASE_NAME}.${DTB_FILE#*.} ${DEPLOYDIR}/${MACHINE}-system.${DTB_FILE#*.}
			ln -sf ${DTB_BASE_NAME}.${DTB_FILE#*.} ${DEPLOYDIR}/system.${DTB_FILE#*.}
		else
			echo "######"
			echo "${PLNX_DEPLOY_DIR}/${DTB_FILE}"
			install -Dm 0644 ${B}/${DTB_FILE} ${PLNX_DEPLOY_DIR}/${DTB_FILE}
		fi
	done
}
